root = statement* expression? ;



statement = functionDeclaration
          | letDeclaration
          | assignmentStatement
          | expressionStatement ';'
          | flowControlStatement
          ;

expressionStatement = callExpression
                    | returnExpression
                    | breakExpression
                    | continueExpression
                    ;

flowControlStatement = blockExpression
                     | ifExpression
                     | loopExpression
                     ;



functionDeclaration = 'func' NAME '(' parameterList ')' functionBody ;

functionBody = blockExpression
             | '=>' expression ';'
             ;

letDeclaration = 'let' 'mut'? NAME '=' expression ';' ;

parameter = 'mut'? NAME ;

parameterList = (parameter (',' parameter)*)? ;

assignmentStatement = lValue assignmentOperator expression ';' ;

lValue = identifierExpression ;



expression = blockExpression
           | callExpression
           | lambdaExpression
           | tupleExpression
           | parenthesizedExpression
           | ifExpression
           | loopExpression
           | returnExpression
           | breakExpression
           | continueExpression
           | unaryExpression
           | binaryExpression
           | identifierExpression
           | stringExpression
           | boolExpression
           | numberExpression
           | nilExpression
           ;

blockExpression = '{' statement* expression? '}' ;

callExpression = expression '(' (expression (',' expression)*)? ')' ;

lambdaExpression = '(' parameterList ')' '=>' expression ;

tupleExpression = '(' expression ',' expression (',' expression)* ')' ;

parenthesizedExpression = '(' expression ')' ;

ifExpression = 'if' expression blockExpression 'else' blockExpression ;

loopExpression = 'loop' blockExpression ;

returnExpression = 'return' expression? ;

breakExpression = 'break' expression? ;

continueExpression = 'continue' ;

unaryExpression = unaryOperator expression ;

binaryExpression = expression binaryOperator expression ;

identifierExpression = NAME ;

stringExpression = normalString | interpolatedString ;

boolExpression = 'true' | 'false' ;

numberExpression = NUMBER_LITERAL ;

nilExpression = '(' ')' ;



normalString = '"' (STRING_TEXT | '\{' expression '}')* '"' ;

interpolatedString = '\"' (STRING_TEXT | '{' expression '}')* '"' ;



assignmentOperator = '='
                   | '+='
                   | '-='
                   | '*='
                   | '/='
                   ;

unaryOperator = '+'
              | '-'
              | '!'
              ;

binaryOperator = '+'
               | '-'
               | '*'
               | '/'
               | '<'
               | '>'
               | '<='
               | '>='
               | '=='
               | '!='
               ;
