root = statement* ;



statement = functionDeclaration
          | letDeclaration
          | assignmentStatement
          | callExpression      ';'
          | returnExpression    ';'
          | breakExpression     ';'
          | continueExpression  ';'
          | blockExpression
          | ifExpression
          | loopExpression
          ;



functionDeclaration = 'func' NAME '(' parameter* ')' functionBody ;

functionBody = blockExpression
             | '=>' expression ';'
             ;

letDeclaration = 'let' NAME '=' expression ';' ;

parameter = 'mut'? NAME ;

assignmentStatement = lValue '=' expression ';' ;

lValue = identifierExpression ;



expression = blockExpression
           | callExpression
           | lambdaExpression
           | tupleExpression
           | parenthesizedExpression
           | ifExpression
           | loopExpression
           | returnExpression
           | breakExpression
           | continueExpression
           | unaryExpression
           | binaryExpression
           | identifierExpression
           | stringExpression
           | boolExpression
           | numberExpression
           ;

blockExpression = '{' statement* expression? '}' ;

callExpression = expression '(' (expression (',' expression)*)? ')' ;

lambdaExpression = '(' (parameter (',' parameter)*)? ')' '=>' expression ;

tupleExpression = '(' expression ',' expression (',' expression)* ')' ;

parenthesizedExpression = '(' expression ')' ;

ifExpression = 'if' expression blockExpression 'else' blockExpression ;

loopExpression = 'loop' blockExpression ;

returnExpression = 'return' expression? ;

breakExpression = 'break' expression? ;

continueExpression = 'continue' ;

unaryExpression = unaryOperator expression ;

binaryExpression = expression binaryOperator expression ;

identifierExpression = NAME ;

stringExpression = STRING_LITERAL ;

boolExpression = 'true' | 'false' ;

numberExpression = NUMBER_LITERAL ;



unaryOperator = '+'
              | '-'
              | '!'
              ;

binaryOperator = '+'
               | '-'
               | '*'
               | '/'
               | '<'
               | '>'
               | '<='
               | '>='
               | '=='
               | '!='
               ;
